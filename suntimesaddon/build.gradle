apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    defaultConfig {
        //noinspection MinSdkTooLow
        minSdkVersion 14
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion 28
        versionCode 4
        versionName "0.4.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version", "api"
    productFlavors {
        supportlib {
            dimension "version"
            compileSdkVersion 28
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 28
            versionNameSuffix "-supportlib"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
        androidx {
            dimension "version"
            compileSdkVersion 33
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 33
            versionNameSuffix "-androidx"
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        }

        api28 {
            dimension "api"
            compileSdkVersion 28
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 28
            versionNameSuffix "-api28"
        }
        api30 {
            dimension "api"
            compileSdkVersion 30
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 30
            versionNameSuffix "-api30"
        }
        api33 {
            dimension "api"
            compileSdkVersion 33
            //noinspection ExpiredTargetSdkVersion,OldTargetApi
            targetSdkVersion 33
            versionNameSuffix "-api33"
        }
    }
    configurations {
        androidxApi28Implementation {}
        androidxApi30Implementation {}
        androidxApi33Implementation {}
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (project.properties['android.useAndroidX'] && names.contains("supportlib")) {
            setIgnore(true)
        } else if (!project.properties['android.useAndroidX'] && (names.contains("androidx") || names.contains("api30") || names.contains("api33"))) {
            setIgnore(true)
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    supportlibImplementation 'com.android.support:design:28.0.0'
    supportlibImplementation 'com.android.support:appcompat-v7:28.0.0'

    androidxImplementation 'com.google.android.material:material:1.6.0'
    androidxImplementation 'androidx.appcompat:appcompat:1.6.1'

    androidxApi28Implementation 'com.google.android.material:material:1.0.0'
    androidxApi28Implementation 'androidx.appcompat:appcompat:1.0.0'

    androidxApi30Implementation 'com.google.android.material:material:1.6.0'
    androidxApi30Implementation 'androidx.appcompat:appcompat:1.6.1'

    androidxApi33Implementation 'com.google.android.material:material:1.6.0'
    androidxApi33Implementation 'androidx.appcompat:appcompat:1.6.1'

    testImplementation 'junit:junit:4.12'

    if (project.properties['android.useAndroidX']) {
        androidTestImplementation 'androidx.test.ext:junit:1.1.1'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    } else {
        androidTestImplementation 'com.android.support.test:runner:1.0.2'
        androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    }
}

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.forrestguice.suntimesaddon'
            artifactId 'suntimesaddon'
            version '0.4.0'
            artifact(sourceJar)
            artifact ("$buildDir/outputs/aar/suntimesaddon-release.aar") {
                builtBy assemble
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}